// Copyright 2018-2020 Twitter, Inc.
// Licensed under the MoPub SDK License Agreement
// http://www.mopub.com/legal/sdk-license-agreement/

apply plugin: 'com.android.library'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

def siteUrl = 'https://github.com/mopub/mopub-android-sdk'
def gitUrl = 'https://github.com/mopub/mopub-android-sdk.git'
def issuesUrl = 'https://github.com/mopub/mopub-android-sdk/issues'
def bintrayRepo = 'mopub-android-sdk'
def bintrayUserOrg = 'mopub'
// Put your credentials (bintray.user, bintray.apikey) in ~/.gradle/gradle.properties

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

artifacts {
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact sourcesJar

                groupId = project.group
                artifactId = project.ext.projectName
                version = project.version
            }
        }
    }

    bintray {
        user = project.hasProperty("bintray.user") ? project.getProperty("bintray.user") : ""
        key = project.hasProperty("bintray.apikey") ? project.getProperty("bintray.apikey") : ""
        publications = ['release']
        pkg {
            repo = bintrayRepo
            name = project.ext.uploadName
            userOrg = bintrayUserOrg
            vcsUrl = gitUrl
            websiteUrl = siteUrl
            issueTrackerUrl = issuesUrl
            desc = project.description
        }
        dryRun = false
        publish = true
    }
}
